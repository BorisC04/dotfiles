pragma Singleton

import Quickshell
import QtQuick
import Quickshell.Io

Singleton {
    id: root

    property int cpuUsage: 0
    property var lastCpuIdle: 0
    property var lastCpuTotal:0
    property int memUsage: 0
    property Process cpuProc: cpuProc

    Timer {
	interval: 2000
	running: true
	repeat: true
	onTriggered: {
	    cpuProc.running = true
	    memProc.running = true
	}
    }

    Process {
	id: cpuProc
	command: ["sh", "-c", "head -1 /proc/stat"]


	stdout: SplitParser {
	    onRead: data => {
		var p = data.trim().split(/\s+/)
		var idle = parseInt(p[4]) + parseInt(p[5])
		var total = p.slice(1, 8).reduce((a, b) => a + parseInt(b), 0)
		if (root.lastCpuTotal > 0) {
		    root.cpuUsage = Math.round(100 * (1 - (idle - root.lastCpuIdle) / (total - root.lastCpuTotal)))
		}
		root.lastCpuTotal = total
		root.lastCpuIdle = idle
	    }
	}
    }

    Process {
	id: memProc
	command: ["sh", "-c", "free | grep Mem"]
	stdout: SplitParser {
	    onRead: data => {
		var parts = data.trim().split(/\s+/)
		var total = parseInt(parts[1]) || 1
		var used = parseInt(parts[2]) || 0
		root.memUsage = Math.round(100 * used / total)
	    }
	}
	Component.onCompleted: running = true
    }
}
